//file:noinspection UnnecessaryQualifiedReference
plugins {
  id 'java'
  id 'org.springframework.boot' version '3.5.10'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'io.github.tetratheta.bun' version '0.0.22'
}

group = 'io.github.wizwix'
version = '1.0.0'

def frontendDir = layout.projectDirectory.dir('frontend')
def frontendBuildDir = frontendDir.dir('dist')

java {
  sourceCompatibility = JavaLanguageVersion.of(21)
  targetCompatibility = JavaLanguageVersion.of(21)
}

repositories {
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {
  // Spring Boot
  annotationProcessor 'org.projectlombok:lombok'
  compileOnly 'org.projectlombok:lombok'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  runtimeOnly 'com.mysql:mysql-connector-j'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  // JWT (isn't part of Spring Boot ecosystem)
  implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
  runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
  runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'
}

configurations.compileOnly.extendsFrom(configurations.annotationProcessor)

processResources {
  dependsOn('copyFrontendToResources')

  def name = project.name
  def version = project.version.toString()

  // IDEA sucks: still hasn't resolved the wrong warning 'Cannot resolve resource filtering of MatchingCopyAction' for 4 years
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
  with(copySpec {
    from('src/main/resources') {
      include('**/banner.txt')
      expand(application: [title: name, version: version])
    }
  })
}

bootJar.dependsOn('copyFrontendToResources')

tasks.register('copyFrontendToResources', Copy) {
  dependsOn('bunBuild')
  from(frontendBuildDir)
  into(layout.buildDirectory.dir('/resources/main/static'))
}

springBoot { buildInfo() }

test { useJUnitPlatform() }

// bun
bun.forceBun = true
bunInstall.workingDir(frontendDir)
bunBuild.workingDir(frontendDir)

configurations.configureEach {
  resolutionStrategy.cacheChangingModulesFor(0, 'seconds')
}
