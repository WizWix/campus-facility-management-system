//file:noinspection UnnecessaryQualifiedReference
plugins {
  id 'java'
  id 'org.springframework.boot' version '3.5.10'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'io.github.m-segreti.bun' version '0.0.15'
}

group = 'io.github.wizwix'
version = '1.0.0'

def frontendDir = layout.projectDirectory.dir('frontend')
def frontendBuildDir = frontendDir.dir('dist')

java {
  sourceCompatibility = JavaLanguageVersion.of(21)
  targetCompatibility = JavaLanguageVersion.of(21)
}

repositories {
  mavenCentral()
}

dependencies {
  // Spring Boot
  annotationProcessor 'org.projectlombok:lombok'
  compileOnly 'org.projectlombok:lombok'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-security'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  runtimeOnly 'com.mysql:mysql-connector-j'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  // JWT (isn't part of Spring Boot ecosystem)
  implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
  runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
  runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'
}

configurations.compileOnly.extendsFrom(configurations.annotationProcessor)

processResources {
  dependsOn('copyFrontendToResources')

  def name = project.name
  def version = project.version.toString()

  // IDEA sucks: still hasn't resolved the wrong warning 'Cannot resolve resource filtering of MatchingCopyAction' for 4 years
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
  with(copySpec {
    from('src/main/resources') {
      include('**/banner.txt')
      expand(application: [title: name, version: version])
    }
  })
}

bootJar.dependsOn('copyFrontendToResources')

tasks.register('copyFrontendToResources', Copy) {
  dependsOn('bunBuild')
  from(frontendBuildDir)
  into(layout.buildDirectory.dir('/resources/main/static'))
}

springBoot { buildInfo() }

test { useJUnitPlatform() }

// bun
def bunExeProvider = provider {
  def ext = extensions.getByType(io.github.m_segreti.BunExtension)
  def sys = ext.system.getOrElse(io.github.m_segreti.BunSystem.detect())
  def ver = ext.version.getOrElse('latest')

  def normalizedVer = (ver == 'latest') ? 'latest' : ver
  def zipBase = sys.zipName().endsWith('.zip') ? sys.zipName().substring(0, sys.zipName().length() - 4) : sys.zipName()
  return layout.projectDirectory.dir(".gradle/bun/${normalizedVer}/${zipBase}").file(sys.exeName()).asFile
}

bunInstall.enabled = false

tasks.register('bunSafeInstall', Exec) {
  doFirst {
    def exe = bunExeProvider.get()
    if (!exe.exists()) throw new GradleException("Bun executable missing at ${exe.absolutePath}. Ensure bunSetup ran.")
    executable = exe.absolutePath
  }

  group = 'bun'
  dependsOn('bunSetup')

  workingDir = frontendDir

  inputs.file(frontendDir.file('package.json'))

  outputs.file(frontendDir.file('bun.lock'))
  outputs.dir(frontendDir.dir('node_modules'))

  args('install',)
}

tasks.register('bunBuild', Exec) {
  doFirst {
    def exe = bunExeProvider.get()
    if (!exe.exists()) throw new GradleException("Bun executable missing at ${exe.absolutePath}. Ensure bunSetup ran.")
    executable = exe.absolutePath
  }

  group = 'bun'
  dependsOn('bunSafeInstall')

  workingDir = frontendDir

  inputs.dir(frontendDir.dir('public'))
  inputs.dir(frontendDir.dir('src'))
  inputs.file(frontendDir.file('package.json'))
  inputs.file(frontendDir.file('bun.lock'))

  outputs.dir(frontendBuildDir)

  args('--bun', 'run', 'build')
}
